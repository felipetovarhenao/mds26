## define a custom scale
$scale = sort(
    e5 f#5 g#5 a5 b5 c#6 d#6
);
## expand scale to multiple octaves
$scale = for $octave in (-1200 0 1200) collect $scale + $octave;
## get scale size
$scalesize = length($scale);
## specify initial scale degree
$degree = 5;
## possible degree steps 
$steps = 1 -1 2 -2;
$melsize = 16;
$onset = 0;
for $i in 1...$melsize do (
    ## get pitch based on degree
    $pitch = $scale:$degree;
    $buf = ezsampler($pitch);
    $buf.transcribe($onset);
    $onset += 200;
    ## choose scale degree step at random
    $step = choose($steps);
    ## update scale degree
    $degree += $step;
    ## constrain degree to maximum possible steps
    $degree = max(1, min($scalesize, $degree)) 
);
render(@play 1)